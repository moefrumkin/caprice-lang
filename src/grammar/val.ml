
open Lang

include Value.Make (Cdata)

(**
  [is_symbolic v] is [true] if [v] has any non-constant symbolic
    formula in it, recursively, or [v] contains a closure, which
    is not reasoned about and therefore [true] by default.

  This is used to see if recursive functions are called with symbolic
  values when type splaying.
*)
let rec is_symbolic : type a. a t -> bool = fun v ->
  match v with
  | VUnit
  | VGenPoly _
  | VEmptyList
  | VType
  | VTypePoly _
  | VTypeUnit
  | VTypeTop
  | VTypeBottom
  | VTypeInt
  | VTypeBool -> false
  | VInt (_, s) -> not (Smt.Formula.is_const s)
  | VBool (_, s) -> not (Smt.Formula.is_const s)
  (* Recursive cases: is symbolic if any subvalue is *)
  | VVariant { payload = Any v' ; label = _ } -> is_symbolic v'
  | VModule map_body
  | VRecord map_body ->
    Record.Label.Map.exists (fun _ (Any v') -> is_symbolic v') map_body
  | VTuple (Any v1, Any v2) ->
    is_symbolic v1 || is_symbolic v2
  | VListCons { hd = Any v_hd ; tl } ->
    is_symbolic v_hd || is_symbolic tl
  | VTypeList t ->
    is_symbolic t
  | VTypeRecord record_body ->
    Record.Label.Map.exists (fun _ t -> is_symbolic t) record_body
  | VTypeVariant variant_body ->
    Variant.Label.Map.exists (fun _ t -> is_symbolic t) variant_body
  | VTypeTuple (t1, t2) ->
    is_symbolic t1 || is_symbolic t2
  | VTypeSingle Any v ->
    is_symbolic v
  | VTypeFun { domain ; codomain = CodValue t ; mode = _ }
  | VGenFun { funtype = { domain ; codomain = CodValue t ; mode = _ } ; table = _ } ->
    is_symbolic domain || is_symbolic t
  | VOnion items ->
    List.exists (fun (Any v) -> is_symbolic v) items
  | VWrapped { data ; funtype = { domain ; codomain = CodValue t ; mode = _ } } ->
    is_symbolic data || is_symbolic domain || is_symbolic t
  (* Closures cases: assume true, but may want to inspect closure *)
  | VFunClosure _
  | VFunFix _
  | VTypeModule _
  | VLazy _
  | VTypeMu _
  | VTypeRefine _
  | VGenFun { funtype = { domain = _ ; codomain = CodDependent _  ; mode = _ } ; table = _ }
  | VTypeFun { domain = _ ; codomain = CodDependent _ ; mode = _ }
  | VWrapped { data = _ ; funtype = { domain = _ ; codomain = CodDependent _ ; mode = _ } } ->
    true
  | VTypeAppend items -> List.exists is_symbolic items

let is_any_symbolic (Any v) = is_symbolic v

(**
  [does_wrap_matter t] is true if wrapping some value [v] (which
    has type [t] already) with the type [t] could possibly change
    the usage behavior of the wrapped value.

  For example, [does_wrap_matter VTypeInt] is [false] because the
    int wrapper is a no-op.
  Another example: [does_wrap_matter (VTypeRecord _)] is [true] because
    the record wrapper can hide labels in the value.

  This function is used to avoid adding lazy wrappers to lazily-generated
  values.
*)
let rec does_wrap_matter : typ t -> bool = function
  | VType
  | VTypePoly _
  | VTypeUnit
  | VTypeTop
  | VTypeBottom
  | VTypeInt
  | VTypeBool
  | VTypeSingle _ -> false
  (* propagate *)
  | VTypeVariant variant_t ->
    Variant.Label.Map.exists (fun _ -> does_wrap_matter) variant_t
  | VTypeList typ
  | VTypeRefine { typ ; _ } -> does_wrap_matter typ
  | VTypeTuple (t1, t2) -> does_wrap_matter t1 || does_wrap_matter t2
  (* closures (mu), functions, and records/modules need wrap *)
  | VTypeMu _ (* we overapproximate and assume the recursive type wrap can matter *)
  | VTypeFun _ (* function wrapper adds usage checks *)
  | VTypeRecord _ (* record and module wrappers can hide labels *)
  | VTypeModule _
  | VTypeAppend _ -> true

let is_callable : typ t -> bool = function
  | VTypeFun _ -> true
  | _ -> false

(**
  [intensional_equal x y] is [Some (b, s)] if [x] and [y] are of the same
    shape, and their components are of the same shape, where [b] is
    the concrete value of their intensional equality, and [s] is the symbolic
    value.
      e.g. [intensional_equal (0, 1) (1, 0)] is [Some (false, s)] for [s] the
        formula describe equality of all symbolic components, which were not
        written at all in the example concrete tuples.

    It is [None] if they are not of the same shape, which indicates a runtime
    type mismatch.
      e.g. [intensional_equal () int] is a mismatch.
      e.g. [intensional_equal (0, 1) true] is a mismatch.
*)
let rec intensional_equal (x : any) (y : any) : Comparator.t =
  let open Comparator in
  if x == y then make true else
  match x, y with
  (* trivially equal *)
  | Any VUnit, Any VUnit
  | Any VEmptyList, Any VEmptyList
  | Any VType, Any VType
  | Any VTypeUnit, Any VTypeUnit
  | Any VTypeTop, Any VTypeTop
  | Any VTypeBottom, Any VTypeBottom
  | Any VTypeInt, Any VTypeInt
  | Any VTypeBool, Any VTypeBool ->
    make true
  | Any VTypePoly { id = id1 }, Any VTypePoly { id = id2 } ->
    make (id1 = id2)
  (* symbolic equality *)
  | Any VInt (i1, s1), Any VInt (i2, s2) ->
    (i1 = i2, Formula.binop Smt.Binop.Equal s1 s2)
  | Any VBool (b1, s1), Any VBool (b2, s2) ->
    (b1 = b2, Formula.binop Smt.Binop.Equal s1 s2)
  (* propagate equality *)
  | Any VVariant v1, Any VVariant v2 ->
    let= () = Variant.Label.equal v1.label v2.label in
    intensional_equal v1.payload v2.payload
  | Any VListCons { hd = a1 ; tl = tl1 }, Any VListCons { hd = a2 ; tl = tl2 } ->
    let- () = intensional_equal a1 a2 in
    iequal tl1 tl2
  | Any VGenPoly g1, Any VGenPoly g2 ->
    let= () = g1.id = g2.id in
    make (g1.nonce = g2.nonce)
  | Any VTuple (l1, r1), Any VTuple (l2, r2) ->
    let- () = intensional_equal l1 l2 in
    intensional_equal r1 r2
  | Any VGenFun { funtype = f1 ; table = t1 }
  , Any VGenFun { funtype = f2 ; table = t2 } ->
    begin match t1, t2 with
    | Some tbl1, Some tbl2 -> make (Utils.Cell.equal tbl1 tbl2)
    | _ ->
      (* Resort to physical equality when no table because two different
        generated functions may have the same type. Physical equality tells
        different generated functions apart. This is incomplete because it is
        possible that the type of the function only admits one possible function
        (e.g. identity), but we still tell them apart. *)
      make (f1 == f2)
    end
  | Any VTypeSingle v1, Any VTypeSingle v2 ->
    intensional_equal v1 v2
  | Any VTypeList t1, Any VTypeList t2 ->
    iequal t1 t2
  | Any VTypeTuple (tl1, tr1), Any VTypeTuple (tl2, tr2) ->
    let- () = iequal tl1 tl2 in
    iequal tr1 tr2
  | Any VTypeFun tf1, Any VTypeFun tf2 ->
    iequal_ftype tf1 tf2
  | Any VRecord m1, Any VRecord m2
  | Any VModule m1, Any VModule m2 ->
    reduce_lists (fun (l1, v1) (l2, v2) ->
      let= () = Record.Label.equal l1 l2 in
      intensional_equal v1 v2
    ) (Record.Label.Map.to_list m1) (Record.Label.Map.to_list m2)
  | Any VTypeRecord m1, Any VTypeRecord m2 ->
    reduce_lists (fun (l1, v1) (l2, v2) ->
      let= () = Record.Label.equal l1 l2 in
      iequal v1 v2
    ) (Record.Label.Map.to_list m1) (Record.Label.Map.to_list m2)
  | Any VTypeVariant m1, Any VTypeVariant m2 ->
    reduce_lists (fun (l1, v1) (l2, v2) ->
      let= () = Variant.Label.equal l1 l2 in
      iequal v1 v2
    ) (Variant.Label.Map.to_list m1) (Variant.Label.Map.to_list m2)
  | Any VTypeModule c1, Any VTypeModule c2 ->
    let rec fold bindings x y =
      match x, y with
      | [], [] -> make true
      | [], _ | _, [] -> make false
      | (lx, tx) :: xs, (ly, ty) :: ys ->
        let= () = Record.Label.equal lx ly in
        let- () =
          iequal_closure bindings
            { captured = tx ; env = c1.env }
            { captured = ty ; env = c2.env }
        in
        fold [ Record.Label.to_ident lx, Record.Label.to_ident ly ] xs ys
    in
    fold [] c1.captured c2.captured
  | Any VTypeRefine r1, Any VTypeRefine r2 ->
    let- () = iequal r1.typ r2.typ in
    iequal_closure [ r1.var, r2.var ] r1.pred r2.pred
  | Any VFunClosure c1, Any VFunClosure c2 ->
    iequal_closure [ c1.param, c2.param ] c1.closure c2.closure
  | Any VFunFix c1, Any VFunFix c2 ->
    iequal_closure [ c1.fvar, c2.fvar ; c1.param, c2.param ] c1.closure c2.closure
  | Any VTypeMu c1, Any VTypeMu c2 ->
    iequal_closure [ c1.var, c2.var ] c1.closure c2.closure
  | Any VWrapped w1, Any VWrapped w2 ->
    let- () = intensional_equal (Any w1.data) (Any w2.data) in
    iequal_ftype w1.funtype w2.funtype
  | Any VLazy s1, Any VLazy s2 when s1.cell == s2.cell ->
    reduce_lists iequal s1.wrapping_types s2.wrapping_types
  | Any VLazy _, _ | _, Any VLazy _ ->
    (* For now, say false if comparing lazy values. It may be more safe to say shape mismatch. *)
    make false
    (* TODO: eventually we want to handle these by asking for lazy environment *)
  | _, _ ->
    Utils.Etc.assert_uniq_ctor x y;
    make false

and iequal : type a. a t -> a t -> Comparator.t = fun x y ->
  intensional_equal (Any x) (Any y)

and iequal_ftype (tf1 : (typ t, fun_cod) Funtype.t)
  (tf2 : (typ t, fun_cod) Funtype.t) : Comparator.t =
  let open Comparator in
  let= () = Funtype.equal_mode tf1.mode tf2.mode in
  let- () = iequal tf1.domain tf2.domain in
  iequal_cod tf1.codomain tf2.codomain

and iequal_cod cod1 cod2 =
  if cod1 == cod2 then Comparator.make true else
  match cod1, cod2 with
  | CodValue t1, CodValue t2 ->
    intensional_equal (Any t1) (Any t2)
  | CodDependent (id1, c1), CodDependent (id2, c2) ->
    iequal_closure [ id1, id2 ] c1 c2
  | _ ->
    Utils.Etc.assert_uniq_ctor cod1 cod2;
    (* Both are types, so not failure, but never equal because
      a dependent function is not a non-dependent function. *)
    Comparator.make false

(**
  [iequal_closure bindings closure1 closure2] is intensional
    equality on [closure1] and [closure2], supposing the association
    list [bindings] are the equivalent bindings in the expressions
    that overwrite the environments.

  Closure equality will not be a shape mismatch, even if the
  expression references values in the environment of different
  shape in the same spot. Instead, it is just false.

  E.g. This will be false, not a shape mismatch.
    { x |-> 0 } ,
      match x with
      | _ -> true
       end

    { x |-> `None () } ,
       match x with
       | _ -> true
       end
*)
and iequal_closure bindings closure1 closure2 =
  let open Comparator in
  let rec iequal_expr bindings e1 e2 =
    if e1 == e2 && closure1.env == closure2.env then
      (* skip physically equal expressions as long as they are in the same environment *)
      make true
    else
    let ieq = iequal_expr bindings in
    match e1, e2 with
    (* trivially equal *)
    | Ast.EUnit, Ast.EUnit
    | EEmptyList, EEmptyList
    | EPick_i, EPick_i
    | EAbstractType, EAbstractType
    | EType, EType
    | ETypeInt, ETypeInt
    | ETypeBool, ETypeBool
    | ETypeTop, ETypeTop
    | ETypeBottom, ETypeBottom
    | ETypeUnit, ETypeUnit ->
      make true
    | EInt i1, EInt i2 ->
      make (Int.equal i1 i2)
    | EBool b1, EBool b2 ->
      make (Bool.equal b1 b2)
    (* equal values *)
    | EVar id1, EVar id2 ->
      iequal_id bindings id1 id2
    (* propagate equality *)
    | ENot e1, ENot e2
    | EAssert e1, EAssert e2
    | EAssume e1, EAssume e2
    | ETypeList e1, ETypeList e2
    | ETypeSingle e1, ETypeSingle e2 ->
      ieq e1 e2
    | EBinop r1, EBinop r2 ->
      let= () = Binop.equal r1.binop r2.binop in
      let- () = ieq r1.left r2.left in
      ieq r1.right r2.right
    | EIf r1, EIf r2 ->
      let- () = ieq r1.if_ r2.if_ in
      let- () = ieq r1.then_ r2.then_ in
      ieq r1.else_ r2.else_
    | EAppl r1, EAppl r2 ->
      let- () = ieq r1.func r2.func in
      ieq r1.arg r2.arg
    | EProject r1, EProject r2 ->
      let= () = Record.Label.equal r1.label r2.label in
      ieq r1.record r2.record
    | ERecord m1, ERecord m2
    | ETypeRecord m1, ETypeRecord m2 ->
      reduce_lists (fun (l1, e1) (l2, e2) ->
        let= () = Record.Label.equal l1 l2 in
        ieq e1 e2
      ) (Record.Label.Map.to_list m1) (Record.Label.Map.to_list m2)
    | ETuple (l1, r1), ETuple (l2, r2)
    | EListCons { hd = l1 ; tl = r1 }, EListCons { hd = l2 ; tl = r2 } ->
      let- () = ieq l1 l2 in
      ieq r1 r2
    | EVariant r1, EVariant r2 ->
      let= () = Variant.Label.equal r1.label r2.label in
      ieq r1.payload r2.payload
    | ETypeVariant l1, ETypeVariant l2 ->
      reduce_lists (fun r1 r2 ->
        let= () = Variant.Label.equal r1.Variant.label r2.label in
        ieq r1.payload r2.payload
      ) l1 l2
    (* check closures *)
    | EFunction r1, EFunction r2 ->
      iequal_expr ((r1.param, r2.param) :: bindings) r1.body r2.body
    | ELet r1, ELet r2 ->
      let- () = iequal_statement bindings r1.stmt r2.stmt in
      iequal_expr (
        (Ast.id_of_stmt r1.stmt, Ast.id_of_stmt r2.stmt) :: bindings
      ) r1.body r2.body
    | EModule l1, EModule l2 ->
      begin match l1, l2 with
      | [], [] -> make true
      | [], _ | _, [] -> make false
      | s1 :: tl1, s2 :: tl2 ->
        (* compare first statement and continue with remainder of modules *)
        let id1, id2 = Ast.id_of_stmt s1, Ast.id_of_stmt s2 in
        let- () = make (Ident.equal id1 id2) in
        let- () = iequal_statement bindings s1 s2 in
        iequal_expr ((id1, id2) :: bindings) (Ast.EModule tl1) (Ast.EModule tl2)
      end
    | ETypeModule l1, ETypeModule l2 ->
      begin match l1, l2 with
      | [], [] -> make true
      | [], _ | _, [] -> make false
      | (lbl1, t1) :: tl1, (lbl2, t2) :: tl2 ->
        let- () = ieq t1 t2 in
        let= () = Record.Label.equal lbl1 lbl2 in
        let id1 = Record.Label.to_ident lbl1
        and id2 = Record.Label.to_ident lbl2 in
        iequal_expr ((id1, id2) :: bindings) (ETypeModule tl1) (ETypeModule tl2)
      end
    | ETypeRefine r1, ETypeRefine r2 ->
      let- () = ieq r1.typ r2.typ in
      iequal_expr ((r1.var, r2.var) :: bindings) r1.pred r2.pred
    | ETypeMu r1, ETypeMu r2 ->
      iequal_expr ((r1.var, r2.var) :: bindings) r1.body r2.body
    | ETypeFun tf1, ETypeFun tf2 ->
      begin match tf1.domain, tf2.domain with
      | (None, t1), (None, t2) ->
        let- () = ieq t1 t2 in
        ieq tf1.codomain tf2.codomain
      | (Some id1, t1), (Some id2, t2) ->
        let- () = ieq t1 t2 in
        iequal_expr ((id1, id2) :: bindings) tf1.codomain tf2.codomain
      | _ ->
        make false
      end
    | EMatch r1, EMatch r2 ->
      let- () = ieq r1.subject r2.subject in
      reduce_lists (fun (pat1, body1) (pat2, body2) ->
        match check_pattern pat1 pat2 with
        | Some bindings' ->
          iequal_expr (bindings' @ bindings) body1 body2
        | None ->
          make false
      ) r1.patterns r2.patterns
    | _ ->
      Utils.Etc.assert_uniq_ctor e1 e2;
      make false

  (*
    Compare statements like let-expressions. A body is required.
    Requires equal names.
  *)
  and iequal_statement bindings s1 s2 =
    match s1, s2 with
    | SLet r1, SLet r2 ->
      let- () = iequal_expr bindings r1.defn r2.defn in
      iequal_annot bindings r1.annot r2.annot
    | SLetRec r1, SLetRec r2 ->
      let- () = iequal_expr ((r1.param, r2.param) :: (r1.name, r2.name) :: bindings) r1.defn r2.defn in
      iequal_annot bindings r1.annot r2.annot
    | _ ->
      Utils.Etc.assert_uniq_ctor s1 s2;
      make false

  and iequal_id bindings id1 id2 =
    let de_bruijn_eq =
      List.find_map (fun (d1, d2) ->
        if Ident.equal id1 d1 then
          (* Found bound in left original expression. Make sure also in right *)
          Some (make (Ident.equal id2 d2))
        else if Ident.equal id2 d2 then
          (* Found bound in right but not in left, so these idents are not equal *)
          Some (make false)
        else
          None
      ) bindings
    in
    match de_bruijn_eq with
    | Some res ->
      (* We have an answer by looking in the bindings *)
      res
    | None ->
      (* The variables are supposed to bound in the environment *)
      begin match Env.find id1 closure1.env, Env.find id2 closure2.env with
      | Some v1, Some v2 ->
        (* Found the values. Now compare, and turn shape mismatches into false *)
        intensional_equal v1 v2
      | None, None ->
        (* Both are not bound. This is strange, but technically they can be equal *)
        make (Ident.equal id1 id2)
      | _ ->
        (* Exactly one of the variables was not bound, so they cannot be equal *)
        make false
      end

  (* check that types annotations on variables are the same *)
  and iequal_annot bindings annot1 annot2 =
    match annot1, annot2 with
    | Lang.Ast.ANone, Lang.Ast.ANone ->
      make true
    | AType { typ = t1 ; do_check = _ } , AType { typ = t2 ; do_check = _ } ->
      iequal_expr bindings t1 t2
    | _ ->
      Utils.Etc.assert_uniq_ctor annot1 annot2;
      make false

  (** Check that patterns are equal, returning [None] if they cannot
    be equal and [Some bindings] if they are, as well as the assocation
    list [bindings] that are equal in an expression bodies depending on
    the patterns. *)
  and check_pattern p1 p2 =
    match p1, p2 with
    | Pattern.PAny, Pattern.PAny
    | PUnit, PUnit
    | PEmptyList, PEmptyList ->
      Some []
      (* iequal_expr bindings body1 body2 *)
    | PVariable id1, PVariable id2 ->
      Some [id1, id2]
    | PVariant v1, PVariant v2 ->
      if Variant.Label.equal v1.label v2.label then
        check_pattern v1.payload v2.payload
      else
        None
    | PTuple (l1, r1), PTuple (l2, r2)
    | PDestructList (l1, r1), PDestructList (l2, r2) ->
      check_several_patterns [ l1 ; r1 ] [ l2 ; r2 ]
    | PPatternOr ls1, PPatternOr ls2 ->
      check_several_patterns ls1 ls2
    | PPatternAs (pat1, id1), PPatternAs (pat2, id2) ->
      Option.map (fun ls ->
        (id1, id2) :: ls
      ) (check_pattern pat1 pat2)
    | _ ->
      Utils.Etc.assert_uniq_ctor p1 p2;
      None

  and check_several_patterns l1 l2 =
    List.fold_left2 (fun acc_m p1 p2 ->
      Option.bind acc_m (fun b ->
        Option.bind (check_pattern p1 p2) (fun b' ->
          Some (b @ b')
        )
      )
    ) (Some []) l1 l2

  in

  iequal_expr bindings closure1.captured closure2.captured

(* This is slower than it could be, but I'm saving code and functor
  complexity by implementing it only in the heavy way, above. *)
let equal_any x y =
  let (b, _) = intensional_equal x y in b

(**
  [equal v1 v2] is intensional equality of [v1] and [v2].
*)
let equal (type a) (x : a t) (y : a t) : bool =
  equal_any (Any x) (Any y)

let equal_fun_cod cod1 cod2 =
  let (b, _) = iequal_cod cod1 cod2 in b

let equal_closure c1 c2 =
  let (b, _) = iequal_closure [] c1 c2 in b

let rec labels (x : any): Record.Label.Set.t =
  match x with 
  | Any (VRecord record_body) -> (Record.label_set record_body)
  | Any (VOnion items) -> List.map labels items |> List.fold_left (fun a b -> Record.Label.Set.union b a) Record.Label.Set.empty
  | _ -> Record.Label.Set.empty
